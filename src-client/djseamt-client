#!/usr/bin/perl


##############################################################################
#
#    Copyright (C) 2011  Bernd Arnold - https://github.com/wopfel
#
#    This file is part of DJSEAMT.
#    https://github.com/wopfel/djseamt
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License version 2 as 
#    published by the Free Software Foundation.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License along
#    with this program; if not, write to the Free Software Foundation, Inc.,
#    51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#
##############################################################################


use strict;
use warnings;
use Getopt::Long;
use Config::IniFiles;
use UUID::Tiny;
use LWP::UserAgent;
use URI::Escape;
use Sys::Hostname;
use XML::Simple;
use Data::Dumper;

my $CLIENT_VERSION = "0.0.0.1";

my ( $help, $configfilename
   );



################################################################
##
##  Process command line arguments
##
################################################################

my $arg_status = GetOptions(
                             "h"   => \$help,           "help"         => \$help,
                             "c=s" => \$configfilename, "configfile=s" => \$configfilename,
                           );

print_syntax() if $help;
print_syntax( "Missing config file" )  unless defined $configfilename;


################################################################
##
##  Read master config file
##
################################################################

print "Reading master config file '$configfilename'...\n";

my $master_cfg = new Config::IniFiles( -file => $configfilename );

if ( ! defined $master_cfg ) { print "Error loading master config file.\n"; exit 1; }

my $configdir = $master_cfg->val( 'client', 'configdir' );

# There's no sense proceeding when the directory doesn't exist
die "Configuration directory doesn't exist" unless -d $configdir;




################################################################
##
##  Setting up configuration
##
################################################################

print "Setting up configuration...\n";

if ( ! -e "$configdir/id" ) {

    print "Generating new id...\n";

    my $new_uuid = create_UUID_as_string();

    open IDFILE, ">", "$configdir/id"  or  die "Cannot create id file '$configdir/id'";
    print IDFILE "[id]\n";
    print IDFILE "\tuuid = $new_uuid\n";
    print IDFILE "\ttimestamp_generated = ", time, "\n";
    close IDFILE;

}

my $id_cfg = new Config::IniFiles( -file => "$configdir/id" );

if ( ! defined $id_cfg ) { print "Error loading id config file.\n"; exit 1; }

my $client_id = $id_cfg->val( 'id', 'uuid' );

print "Using client uuid '$client_id'.\n";




################################################################
##
##  Searching for sub configuration files
##
################################################################

print "Searching for sub configuration files in '$configdir'...\n";

my %subconfigs;

opendir DIR, $configdir;

for ( readdir( DIR ) ) {

    # Only accept this pattern: file name begins with 'subconfig-', followed by a 4 digit number
    next unless $_ =~ /^subconfig-\d{4}/;

    my $sub_cfg = new Config::IniFiles( -file => "$configdir/$_" );

    if ( ! defined $sub_cfg ) { warn "Error loading subconfig file '$_'"; next; }

    my $priority = $sub_cfg->val( 'subconfig', 'priority' );

    print "Found subconfig file '$_', priority $priority.\n";

    # Ignore subconfig files with priority = 0
    next unless $priority > 0;

    $subconfigs{ $_ }  = {
                             added                      => time,
                             last_connection_try        => 0,
                             last_successful_connection => 0,
                             priority                   => $priority,
                         };

}

closedir DIR;

print "Found ", scalar(keys %subconfigs), " subconfig files.\n";

die "Need at least one subconfig file to proceed" unless scalar keys %subconfigs >= 1;

# Stores the subconfig filename which leads to a successful handshake
my $active_subconfig_name;

# Stores timestamp of the last successful handshake
my $last_successful_handshake_timestamp;

# Find a suitable subconfig file in an endless loop
while (1) {

    # Sort the subconfigs
    # Ranking:
    # - try subconfigs with the oldest last connection try time first (beginning with 0, the initial value)
    # - try highest priority value first
    my @sorted_subconfigs = sort { $subconfigs{$b}{last_connection_try} <=> $subconfigs{$a}{last_connection_try}
                                   or
                                   $subconfigs{$a}{priority} <=> $subconfigs{$b}{priority}
                                } keys %subconfigs;

    my $use_subconfig_name = pop @sorted_subconfigs;

    print "Trying subconfig file '$use_subconfig_name' now.\n";

    # Remember when we tried a connection with this subconfig file
    $subconfigs{$use_subconfig_name}{last_connection_try} = time;

    # End the loop when the handshake was successful
    handshake( $use_subconfig_name ) and last;

    warn "Warning: unsuccessful handshake, trying next subconfig file (if any).\n";

}

die "Error: no connection could be established - terminating" unless defined $active_subconfig_name;




################################################################
##
##  Main loop
##
################################################################

print "Entering main loop...\n";

my $end_program = 0;

while ( $end_program == 0 ) {

    # Is it handshake time?
    handshake() if time - $last_successful_handshake_timestamp > 10;

    # So we don't eat up the CPU
    sleep 5;

}




################################################################
##
##  Program ends
##
################################################################

print "Terminating client...\n";

exit 0;




################################################################
##
##  Sub: Handshake with webserver
##
##  Parameter: subconfig filename as a string (uses active subconfig name when omitted)
##  Returns: undef, when no successful connection could be established,
##           a true value otherwise
##
################################################################

sub handshake {

    my $subconfig_name = shift || $active_subconfig_name;

    return undef unless defined $subconfig_name;

    my $sub_cfg = new Config::IniFiles( -file => "$configdir/$subconfig_name" );

    if ( ! defined $sub_cfg ) { warn "Error loading subconfig file '$subconfig_name'"; return 0; }

    my %postcontent;
    $postcontent{'id'} = $client_id;
    $postcontent{'id_generated'} = $id_cfg->val( 'id', 'timestamp_generated' );
    $postcontent{'client_version'} = $CLIENT_VERSION;
    $postcontent{'hostname'} = hostname;

    my $ua = LWP::UserAgent->new;
    $ua->agent( "djseamt-client/$CLIENT_VERSION" );

    my $request_url = $sub_cfg->val( 'subconfig', 'contact_url' ) . "/hello.php";

    # Setup request, encode POST data
    my $req = HTTP::Request->new( POST => $request_url );
    $req->content_type( 'application/x-www-form-urlencoded' );
    $postcontent{$_} = uri_escape( $postcontent{$_} ) foreach keys %postcontent;
    my @postcontent;
    push @postcontent, "$_=$postcontent{$_}" foreach keys %postcontent;
    $req->content( join  "&", @postcontent );

    print "Trying to contact server '$request_url'...\n";

    # Send HTTP POST request
    my $response = $ua->request( $req );

    if ( ! $response->is_success ) {
        print "Error communicating with server, '$request_url'.\n";
        return undef;
    }

    print "Successful connection with server.\n";

    my $answer;
    # Using eval, so script doesn't die when invalid data is received
    eval { $answer = XMLin( $response->content ) };
    # Invalid XML data?
    if ( ! $answer ) {
        print "Error: invalid XML data received.\n";
        return undef;
    }
    # Check protocol version
    my $protocol_version = $answer->{protocol}->{version};
    if ( $protocol_version !~ /^\d+$/ ) {
        print "Error: invalid protocol version.\n";
        return undef;
    }
    # Check origin
    if ( $answer->{sender}->{module} ne "hello" ) {
        print "Error: invalid module origin.\n";
        return undef;
    }
    # TODO: validate other fields of repsonse too

    my $status = $answer->{status};

    print "Received $status answer from webserver '$answer->{sender}->{name}/$answer->{sender}->{version}'.\n";

    # Don't use this subconfig file when webserver didn't give as an OK message
    if ( $status ne "OK" ) {
        print "Error: webserver reported error.\n";
        return undef;
    }

    print "Successful handshake.\n";
    print Dumper( $answer );
    $last_successful_handshake_timestamp = time;

    if ( $subconfig_name ne $active_subconfig_name ) {
        $active_subconfig_name = $subconfig_name;
        print "Using subconfig file '$active_subconfig_name' now.\n";
    }

    # Remember the last successful connection time of this subconfig file
    # So we can sort on the last successful connection time in the future (new subconfig files during run-time)
    $subconfigs{$subconfig_name}{last_successful_connection} = time;

    return 1;

}




################################################################
##
##  Sub: Print syntax help
##
##  Parameter: Error message (optional, but appreciated)
##
################################################################

sub print_syntax {

    my $error = shift || "unknown";

    print "Error: $error.\n"  unless $help;

    print "DJSEAMT client (https://github.com/wopfel/djseamt)\n";
    print "Copyright (C) 2011 Bernd Arnold\n\n";

    print "DJSEAMT comes with ABSOLUTELY NO WARRANTY; for details see LICENSE file.\n";
    print "This is free software, and you are welcome to redistribute it\n";
    print "under certain conditions; see LICENSE file for details.\n\n";

    print "Distributed Job Scheduling Environment And Management Tool\n\n";

    print "Syntax:\n";
    print "          djseamt-client -c <configfile>\n";
    print "Options:\n";
    print "  -c, --configfile=NAME     use NAME as config file\n";
    print "  -h, --help                show this help\n";

    exit 1;

}

